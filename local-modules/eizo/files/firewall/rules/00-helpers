# -*- sh -*-

function ip46tables () {
    iptables "$@"

    # We replace `-o internet` by `-o internet6`. That's a bit
    # difficult if we want to keep spaces.
    local -a args
    local last
    while (( $# > 0 )); do
        case "$1,$last" in
            internet,-o)
                args=($args internet6)
                ;;
            *)
                args=($args $1)
                ;;
        esac
        last=$1
        shift
    done
    ip6tables $args
}

function reject () {
    chain=$1 ; shift

    # Those are noisy stuff
    ip46tables -A $chain -p tcp -m multiport --dport 135,137,138,139,445 -j DROP
    ip46tables -A $chain -p udp --sport 5353 --dport 5353 -j DROP   # mDNS
    ip46tables -A $chain -p udp --sport 57621 --dport 57621 -j DROP # Spotify
    # Handle multicast/broadcast differently
    ip46tables -N $chain-MC
    iptables   -A $chain -m addrtype --dst-type BROADCAST -j $chain-MC
    ip46tables -A $chain -m addrtype --dst-type MULTICAST -j $chain-MC
    ip46tables -A $chain-MC -m limit --limit 5/s --limit-burst 5 \
        -j NFLOG --nflog-group 10${1:-1} \
        --nflog-prefix "NF: $chain-reject-mc: "
    ip46tables -A $chain-MC -j DROP
    # Log the remaining
    ip46tables -A $chain -m limit --limit 5/s --limit-burst 5 \
        -j NFLOG --nflog-group ${1:-1} \
        --nflog-prefix "NF: $chain-reject: "
    # And reject
    ip46tables -A $chain -p tcp -j REJECT --reject-with tcp-reset
    iptables   -A $chain -p udp -j REJECT --reject-with icmp-admin-prohibited
    ip6tables  -A $chain -p udp -j REJECT --reject-with icmp6-adm-prohibited
    ip46tables -A $chain -j DROP
}

function dhcp () {
    chain=$1 ; shift

    iptables   -A $chain-INPUT -p udp --source-port bootpc --destination-port bootps -j ACCEPT
    ip6tables  -A $chain-INPUT -p udp --source-port dhcpv6-client --destination-port dhcpv6-server -j ACCEPT
    ip6tables  -A $chain-INPUT -p icmpv6 --icmpv6-type router-solicitation -j ACCEPT
    ip46tables -A $chain-INPUT -p udp --destination-port ntp -j ACCEPT
    ip46tables -A $chain-INPUT -p udp --destination-port domain -j ACCEPT
    ip46tables -A $chain-INPUT -p tcp --destination-port domain -j ACCEPT
}

function icmp () {
    chain=$1 ; shift
    limit=${2:-100}
    iptables  -A $chain -p icmp --icmp-type echo-request -m limit --limit ${limit}/s -j ACCEPT
    iptables  -A $chain -p icmp --icmp-type echo-request -j DROP
    ip6tables -A $chain -p icmpv6 --icmpv6-type echo-request -m limit --limit ${limit}/s -j ACCEPT
    ip6tables -A $chain -p icmpv6 --icmpv6-type echo-request -j DROP
}

function first () {
    net=$1 ; shift
    IFS=. read -r m1 m2 m3 m4 <<< ${net%/*}
    printf "%d.%d.%d.%d/%d\n" $m1 $m2 $m3 $(($m4 + 1)) ${net#*/}
}
function first6 () {
    net=$1 ; shift
    echo ${net%/*}1${net#*/}
}

# Build some kind of address book
typeset -A ip
typeset -a lans
() {

    # Grab stuff from DHCP lease
    function from-dhcp-lease () {
        tac /var/lib/dhcp/dhclient.internet.leases | \
            sed -n -e '/lease {/,$d' -e "s/  $1"' \(.*\);/\1/p'
    }

    # Firewall
    ip[firewall]=$(from-dhcp-lease "fixed-address")

    . /etc/firewall/ips

    setopt extendedglob
    lans=(${(M)${(kOn)ip}:#(lan|wlan)*}) # This is sorted, so should be stable

}
